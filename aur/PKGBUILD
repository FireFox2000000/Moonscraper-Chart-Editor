# Maintainer: bmwalters <oss at walters dot app>

# NOTE:
# This package does not require a Unity engine license file to build, but you 
# may add one:
# - Copy an existing activated license file to Unity_lic.ulf next to PKGBUILD
# - `echo "request" > Unity_lic.ulf` to begin the license activation process

pkgname=moonscraper-chart-editor-git
_pkgname=moonscraper-chart-editor
pkgver=1.6.0.u6000.0.62f1
pkgrel=1
pkgdesc="A song editor for Guitar Hero style rhythm games made in Unity"
url='https://github.com/FireFox2000000/moonscraper-chart-editor'
license=(BSD)
arch=(x86_64)
depends=('mono' 'sdl2' 'ffmpeg' 'libx11' 'gtk3' 'libbass' 'libbass_fx' 'libbassopus')
makedepends=('git' 'unityhub' 'cmake'
  # runtime dependencies for Unity3d
  'desktop-file-utils' 'xdg-utils' 'gcc-libs' 'gconf' 'nss'
  'libgl' 'glu' 'libpng12' 'libxtst' 'libpqxx' 'npm' 'intel-tbb'
  # used to isolate home directory files modified by Unity3d
  'firejail')
provides=(moonscraper-chart-editor)
backup=("opt/$pkgname/local_events.txt"
        "opt/$pkgname/global_events.txt"
        "opt/$pkgname/Custom Resources/settings.ini")
source=('git+https://github.com/FireFox2000000/moonscraper-chart-editor.git#branch=master'
        "$_pkgname.desktop")
sha256sums=('SKIP'
            '393ebe1ee7df6b667d2ff85b82eb1a58c2df7aa8728790cbb260bfa340746a54')
_unity3d_version=6000.0.62f1
_unity3d_changeset=f99f05b3e950

pkgver() {
  cd "$_pkgname"

  printf "%s" "$(git describe --long --tags --always | sed 's/\([^-]*-g\)/r\1/;s/-/./g')"
}

prepare() {
  # Remove vendored libbass in favor of libbass dependency
  rm -rf "$_pkgname/Moonscraper Chart Editor/Assets/Plugins/Bass Audio/Linux/x64"
  rm -f "$_pkgname/Moonscraper Chart Editor/Assets/Plugins/Bass Audio/Linux/x64.meta"
}

build() {
  if [ -f Unity_lic.ulf ]; then
    cp Unity_lic.ulf Unity_lic_physical.ulf
  fi

  # Install Unity via unityhub in firejail to avoid clobbering user's existing unityhub configuration.
  firejail --noprofile --whitelist="$(pwd)" sh <<EOF
  # Install appropriate unity3d version via unityhub
  if [ ! -d "$(pwd)"/unity3d/$_unity3d_version ]; then
    mkdir -p unity3d
    unityhub --no-sandbox --headless install-path --set "$(pwd)"/unity3d
    unityhub --no-sandbox --headless install --version $_unity3d_version --changeset $_unity3d_changeset --architecture x86_64
  fi

  # https://github.com/mono/mono/issues/6752#issuecomment-365212655
  export TERM=xterm

  # Request a manual activation file if the user has no license.
  if [ -f Unity_lic_physical.ulf ] && [ "$(cat Unity_lic_physical.ulf)" = "request" ]; then
    "$(pwd)"/unity3d/$_unity3d_version/Editor/Unity \
      -batchmode \
      -nographics \
      -silent-crashes \
      -createManualActivationFile \
      -logFile - \
      -quit
    echo Manual activation file written to "$srcdir"/Unity_v$_unity3d_version.alf.
    echo Activate license, save to Unity_lic.ulf, then rerun makepkg.
    echo https://docs.unity3d.com/6000.0/Documentation/Manual/ManualActivationCmdMac.html#request-ulf
    exit 1
  fi

  # Activate the license file.
  if [ -f Unity_lic_physical.ulf ]; then
    "$(pwd)"/unity3d/$_unity3d_version/Editor/Unity \
      -batchmode \
      -manualLicenseFile Unity_lic_physical.ulf \
      -nographics \
      -silent-crashes \
      -logFile - \
      -quit
  fi

  # Set path to 7z
  export SEVENZIP=$(which 7z)

  # Build the application using Unity in batch mode
  "$(pwd)"/unity3d/$_unity3d_version/Editor/Unity \
    -batchmode \
    -nographics \
    -silent-crashes \
    -logFile - \
    -projectPath "$_pkgname/Moonscraper Chart Editor" \
    -executeMethod BuildManager.BuildLinux \
    -moonscraperBuildPath "$(pwd)"/build \
    -quit
EOF
}

package() {
  # Install application to /opt
  install -d "$pkgdir"/opt/$pkgname

  VERSION=$(grep -oP 'bundleVersion: \K(.+)' < "$srcdir/$_pkgname/Moonscraper Chart Editor/ProjectSettings/ProjectSettings.asset")
  cp -a "build/Moonscraper Chart Editor v$VERSION Linux (64 bit)/." "$pkgdir"/opt/$pkgname

  # Install binary symlink and desktop files
  install -d "$pkgdir"/usr/{bin,share/{pixmaps,applications}}

  cat << EOF > "$pkgdir/usr/bin/$_pkgname"
#!/bin/sh
exec "/opt/$pkgname/Moonscraper Chart Editor.$CARCH" "\$@"
EOF
  chmod 755 "$pkgdir/usr/bin/$_pkgname"

  install $_pkgname.desktop "$pkgdir"/usr/share/applications
  ln -s "/opt/$pkgname/Moonscraper Chart Editor_Data/Resources/UnityPlayer.png" "$pkgdir"/usr/share/pixmaps/$_pkgname.png

  # Install licenses
  install -d "$pkgdir"/usr/share/licenses

  install -Dm 644 "$_pkgname/LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm 644 "$_pkgname/Moonscraper Chart Editor/Assets/Documentation/attribution.txt" "$pkgdir"/usr/share/licenses/$pkgname/attribution.txt
}
